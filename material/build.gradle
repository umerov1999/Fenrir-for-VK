plugins {
    id("com.android.library")
}

//1.7.0-beta01

def srcDirs = [
        'com/google/android/material/animation',
        'com/google/android/material/appbar',
        'com/google/android/material/badge',
        'com/google/android/material/behavior',
        'com/google/android/material/bottomappbar',
        'com/google/android/material/bottomnavigation',
        'com/google/android/material/bottomsheet',
        'com/google/android/material/button',
        'com/google/android/material/canvas',
        'com/google/android/material/card',
        'com/google/android/material/checkbox',
        'com/google/android/material/chip',
        'com/google/android/material/circularreveal',
        'com/google/android/material/circularreveal/cardview',
        'com/google/android/material/circularreveal/coordinatorlayout',
        'com/google/android/material/color',
        'com/google/android/material/datepicker',
        'com/google/android/material/dialog',
        'com/google/android/material/divider',
        'com/google/android/material/drawable',
        'com/google/android/material/elevation',
        'com/google/android/material/expandable',
        'com/google/android/material/floatingactionbutton',
        'com/google/android/material/imageview',
        'com/google/android/material/internal',
        'com/google/android/material/materialswitch',
        'com/google/android/material/math',
        'com/google/android/material/menu',
        'com/google/android/material/motion',
        'com/google/android/material/navigation',
        'com/google/android/material/navigationrail',
        'com/google/android/material/progressindicator',
        'com/google/android/material/radiobutton',
        'com/google/android/material/resources',
        'com/google/android/material/ripple',
        'com/google/android/material/shape',
        'com/google/android/material/shadow',
        'com/google/android/material/slider',
        'com/google/android/material/snackbar',
        'com/google/android/material/stateful',
        'com/google/android/material/switchmaterial',
        'com/google/android/material/tabs',
        'com/google/android/material/textfield',
        'com/google/android/material/textview',
        'com/google/android/material/theme',
        'com/google/android/material/theme/overlay',
        'com/google/android/material/timepicker',
        'com/google/android/material/tooltip',
        'com/google/android/material/transition',
        'com/google/android/material/transformation',
        'com/google/android/material/typography',
]

android {
    namespace "com.google.android.material"
    sourceSets {
        main.java.srcDir 'java'
        main.java.includes = srcDirs.collect { it + '/**/*.java' }
        main.java.excludes = [
                '**/build/**',
        ]
        srcDirs.forEach {
            main.res.srcDirs += 'java/' + it + '/res'
            main.res.srcDirs += 'java/' + it + '/res-public'
        }
    }
    compileSdk = appCompileSDK
    buildToolsVersion = appBuildTools

    defaultConfig {
        minSdk = appMinSDK
        targetSdk = appTargetSDK
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
        encoding = "utf-8"
    }
}

dependencies {
    api("androidx.annotation:annotation:$annotationVersion")
    api("androidx.appcompat:appcompat:$appcompatVersion")
    api("androidx.core:core:$coreVersion")
    api("androidx.activity:activity:$activityVersion")
    api("androidx.fragment:fragment:$fragmentVersion")
    api("androidx.cardview:cardview:$cardviewVersion")
    api("androidx.dynamicanimation:dynamicanimation:$dynamicanimationVersion")
    api("androidx.constraintlayout:constraintlayout:$constraintlayoutVersion")
    //api("androidx.recyclerview:recyclerview:$recyclerviewVersion")
    api("androidx.coordinatorlayout:coordinatorlayout:$coordinatorlayoutVersion")
    api("androidx.vectordrawable:vectordrawable:$vectordrawableVersion")
    api("androidx.transition:transition:$transitionVersion")
    api("androidx.lifecycle:lifecycle-runtime:$lifecycleVersion")
    api("androidx.drawerlayout:drawerlayout:$drawerlayoutVersion")
    implementation("com.google.errorprone:error_prone_annotations:${errorproneVersion}")
    api project(path: ":viewpager2")
}
